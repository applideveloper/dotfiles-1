# z
_Z_CMD=j
_Z_DATA=~/.z/.z

# anyframe
bindkey '^r' anyframe-widget-execute-history

bindkey '^xi' anyframe-widget-put-history
bindkey '^x^i' anyframe-widget-put-history

bindkey '^g' anyframe-widget-cd-ghq-repository

bindkey '^xk' anyframe-widget-kill
bindkey '^x^k' anyframe-widget-kill

bindkey '^xb' anyframe-widget-checkout-git-branch
bindkey '^x^b' anyframe-widget-checkout-git-branch

peco-jump-dir() {
  local DIR=${BUFFER#cd }
  find -E ${DIR:-.} -type d -a ! -regex '.*.(git|svn)/.*' -a ! -name '.' 2> /dev/null \
    | anyframe-selector-peco \
    | anyframe-action-execute cd
}
zle -N peco-jump-dir
bindkey "^xj" peco-jump-dir
bindkey "^x^j" peco-jump-dir

peco-launchctl() {
  if (( ! $+commands[launchctl] )); then
    return
  fi

  local SERVICE_NAME=$(launchctl list 2>&1 \
      | anyframe-selector-peco \
      | awk '{print $3}')

  local -a ACTIONS
  ACTIONS=(start stop restart reload)
  local ACTION=$(echo -n "${(j:\n:)ACTIONS}" | peco --prompt='Action>')
  if [ -z "$ACTION" ]; then
    return 1
  fi

  echo "$ACTION $SERVICE_NAME" | anyframe-action-execute launchctl
}
zle -N peco-launchctl
bindkey '^xl' peco-launchctl
bindkey '^x^l' peco-launchctl

peco-find() {
  find * -path '*/\.*' -prune -o -type f -print -o -type l -print 2> /dev/null \
    | anyframe-selector-peco \
    | anyframe-action-insert
}
zle -N peco-find
bindkey "^xf" peco-find
bindkey "^x^f" peco-find

peco-ssh-host() {
  grep -iE '^host[[:space:]]+[^*]' ~/.ssh/config \
    | awk '{print $2}' \
    | anyframe-selector-peco \
    | anyframe-action-insert
}
zle -N peco-ssh-host
bindkey '^xs' peco-ssh-host
bindkey '^x^s' peco-ssh-host


# load saved zgen settings
. $GOPATH/src/github.com/tarjoilija/zgen/zgen.zsh


# zsh-completions
compdef mosh=ssh

# display svn branch info
zstyle ':vcs_info:*' enable git svn
zstyle ':vcs_info:svn*' formats ' %F{magenta}%s%f %b'
zstyle ':vcs_info:svn*' branchformat '%b:r%r'
zstyle ':vcs_info:svn+set-branch-format:*' hooks svn-hook
+vi-svn-hook() {
  hook_com[branch]=`svn info | perl -ne 's/^Relative URL: // && print'`
}

# zsh-autosuggestions
zle-line-init() {
  zle autosuggest-start
}
zle -N zle-line-init
unset COMPLETION_WAITING_DOTS
AUTOSUGGESTION_HIGHLIGHT_COLOR='fg=1'
bindkey '^T' autosuggest-toggle

if ! zgen saved; then
  zgen load zsh-users/zsh-syntax-highlighting
  zgen load zsh-users/zsh-completions
  zgen load tarruda/zsh-autosuggestions
  zgen load mollifier/anyframe
  zgen load rupa/z
  zgen load mafredri/zsh-async
  zgen load sindresorhus/pure
  zgen save
fi
